/*
 * Copyright 2025 Yusuf Ren√ßber
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

class SharedPtr<T> {
    priv ptr: *mut T = null,
    priv count: *mut u64 = null
} impl {
    SharedPtr(this: &mut SharedPtr<T>) {
        this.SharedPtr(new T);
    }

    SharedPtr(this: &mut SharedPtr<T>, ptr: *mut T) {
        this.ptr = ptr;

        if ptr != null {
            this.count = new u64;
            *this.count = 1u64;
        } else {
            this.count = null;
        }
    }

    SharedPtr(this: &mut SharedPtr<T>, other: &SharedPtr<T>) {
        (this.ptr, this.count) = (other.ptr, other.count);

        if this.count != null {
            *this.count += 1u64;
        }
    }

    ~SharedPtr(this: &mut SharedPtr<T>) {
        this.Release();
    }

    pub Ptr(this: &SharedPtr<T>) -> *mut T { ret this.ptr; }
    pub Count(this: &SharedPtr<T>) -> u64 { ret (this.count != null) ? (*this.count) : 0u64; }

    priv Release(this: &mut SharedPtr<T>) {
        if (this.count != null) {
            *this.count -= 1u64;

            if (*this.count == 0u64) {
                del this.ptr;
                del this.count;
            }

            (this.ptr, this.count) = (null, null);
        }
    }

    pub op=(this: &mut SharedPtr<T>, other: &SharedPtr<T>) -> &mut SharedPtr<T>{
        if this != other {
            this.Release();
            (this.ptr, this.count) = (other.ptr, other.count);
            if this.count != null { *this.count += 1u64; }
        } ret this;
    }
};