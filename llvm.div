include "divio", "divstr", "divvec" : *;

enum InstructionType {
    Alloca, Store, Load, Ret
};

class Instruction {
    pub _type: InstructionType,
    pub arguments: Vector<String*>
} impl {
    pub Dump() -> String {
        let value: String = "";

        if _type == InstructionType::Alloca {
            value += "%\"";
            value += arguments.Data()[0]->Data();
            value += "\" = alloca ";
            value += arguments.Data()[1]->Data();
        } else if _type == InstructionType::Store {
            value += "store ";
            value += arguments.Data()[0]->Data();
            value += ", ";
            value += arguments.Data()[1]->Data();
        } else if _type == InstructionType::Load {
            value += "%\"";
            value += arguments.Data()[0]->Data();
            value += "\" = load ";
            value += arguments.Data()[1]->Data();
        } else if _type == InstructionType::Ret {
            value += "ret ";
            value += arguments.Data()[0]->Data();
        } else {

        }

        ret value;
    }
};

class Block {
    pub name: String,
    pub instructions: Vector<Instruction>
} impl {
    pub Dump() -> String {
        let value: String = "";
        value += name.Data();
        value += ":\n";

        let i: i64 = 0i64;

        while i < instructions.Size() {
            value += "  ";
            value += instructions.Data()[i].Dump().Data();
            value += "\n";
            i += 1i64;
        }

        ret value;
    }
};

class Argument {
    pub name: String,
    pub _type: String,
    pub variadic: bool = false
} impl {
    pub Dump() -> String {
        let value: String = "";
        value += _type.Data();
        value += " %\"";
        value += name.Data();
        value += "\"";
        ret value;
    }
};

class FunctionType {
    pub return: String,
    pub args: Vector<Argument>
};

class Function {
    pub name: String,
    pub blocks: Vector<Block>,
    pub _type: FunctionType
} impl {
    pub Dump() -> String {
        let value: String = "";
        value += "define ";
        value += _type.return.Data();
        value += " @\"";
        value += name.Data();
        value += "\"(";

        let i: i64 = 0i64;

        while i < _type.args.Size() {
            value += _type.args.Data()[i].Dump().Data();
            i += 1i64;
        }

        value += ")";

        if blocks.Size() != 0i64 {
            value += " {\n";
            let i: i64 = 0i64;

            while i < blocks.Size() {
                value += blocks.Data()[i].Dump().Data();
                i += 1i64;
            }

            value += "}\n";
        }

        ret value;
    }
};

class Module {
    pub name: String = "main",
    pub functions: Vector<Function>,
    pub triple: String = "x86_64-pc-windows-msvc"
} impl {
    pub Dump() -> String {
        let value: String = "";
        value += "; ModuleID = \"";
        value += name.Data();
        value += "\"\n";
        value += "target triple = \"";
        value += triple.Data();
        value += "\"\n";
        value += "target dataLayout = \"\"\n\n";

        let i: i64 = 0i64;

        while i < functions.Size() {
            value += functions.Data()[i].Dump().Data();
            i += 1i64;
        }

        ret value;
    }
};

func MergeString(strings: Vector<String>) -> String {
    let string: String = "";
    let i: i64 = 0i64;

    while i < strings.Size() {
        string += strings.Data()[i].Data();
        i += 1i64;

        if i < strings.Size() {
            string += " ";
        }
    }

    ret string;
}

func main(argc: i32, argv[]: i8*) -> i32 {
    let instruction: Instruction;
    instruction._type = InstructionType::Alloca;
    instruction.arguments.PushBack(&({ ".2" } as String));
    instruction.arguments.PushBack(&({ "i32" } as String));

    let block: Block;
    block.name = "entry";
    block.instructions.PushBack(instruction);

    let argument: Argument = {
        { ".1" } as String,
        { "i32" } as String
    };

    let function: Function;
    function.name = "main";
    function._type.return = "i32";
    function._type.args.PushBack(argument);
    function.blocks.PushBack(block);

    let module: Module;
    module.functions.PushBack(function);
    printf("%s", module.Dump().Data());

    let debug: bool = true;
    let mut command: Vector<String>;
    command.PushBack({ "clang-cl" } as String);

    if debug {
        command.PushBack({ "/Zi" } as String);
        command.PushBack({ "/Od" } as String);
    }

    let fileName: String = "main";

    let input: String = fileName.Data();
    input += ".obj";

    let output: String = "/Fe:";
    output += fileName.Data();
    output += ".exe";

    command.PushBack(input);
    command.PushBack(output);

    printf("%s\n", MergeString(command).Data());
    ret 0;
}