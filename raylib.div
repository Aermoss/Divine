include "divio", "divray", "divtime", "divmath" : *;

func ColorFromHSV_Divine(h: f64, s: f64, v: f64) -> Color {
    let c: f64 = v * s;
    let hh: f64 = fmod(h, 360.0f64) / 60.0f64;
    let x: f64 = c * (1.0f64 - fabs(fmod(hh, 2.0f64) - 1.0f64));
    let (mut r1: f64, mut g1: f64, mut b1: f64) = (0.0f64, 0.0f64, 0.0f64);
    let m: f64 = v - c;

    if 0.0f64 <= hh && hh < 1.0f64 { (r1, g1, b1) = (c, x, 0.0f64); }
    else if 1.0f64 <= hh && hh < 2.0f64 { (r1, g1, b1) = (x, c, 0.0f64); }
    else if 2.0f64 <= hh && hh < 3.0f64 { (r1, g1, b1) = (0.0f64, c, x); }
    else if 3.0f64 <= hh && hh < 4.0f64 { (r1, g1, b1) = (0.0f64, x, c); }
    else if 4.0f64 <= hh && hh < 5.0f64 { (r1, g1, b1) = (x, 0.0f64, c); }
    else { (r1, g1, b1) = (c, 0.0f64, x); }

    ret {
        round((r1 + m) * 255.0f64) as u8,
        round((g1 + m) * 255.0f64) as u8,
        round((b1 + m) * 255.0f64) as u8,
        255u8
    } as Color;
}

func main(argc: i32, argv[]: i8*) -> i32 {
    InitWindow(1600i32, 900i32, "Divine");

    let (mut x: f32, mut y: f32, mut w: f32, mut h: f32) = (0.0f32, 0.0f32, 100.0f32, 100.0f32);
    let (mut horizontal: f32, mut vertical: f32, mut speed: f32) = (1.0f32, 1.0f32, 1.0f32);
    let mut state: bool = false;

    printf("CLOCKS_PER_SEC = %d\n", CLOCKS_PER_SEC);

    let (mut hue: f64, mut sat: f64, mut val: f64) = (0.0f64, 1.0f64, 1.0f64);
    let mut last: f64 = GetTime();

    while !WindowShouldClose() {
        let current: f64 = GetTime();
        let delta: f64 = current - last;
        last = current;

        if IsKeyPressed(KeyboardKey::KEY_SPACE) {
            state = !state;
        }

        if x > (GetScreenWidth() as f32) - w || x < 0.0f32 {
            horizontal *= -1.0f32;
        }

        if y > (GetScreenHeight() as f32) - h || y < 0.0f32 {
            vertical *= -1.0f32;
        }

        speed += GetMouseWheelMove() / 10.0f32;

        if speed > 3.0f32 {
            speed = 3.0f32;
        }

        if speed < 0.03f32 {
            speed = 0.03f32;
        }

        x += horizontal * speed * (delta as f32) * 2500.0f32;
        y += vertical * speed * (delta as f32) * 2500.0f32;

        BeginDrawing();

        hue += 0.05f64 * delta * 2500.0f64;
        if hue >= 360.0f64 { hue -= 360.0f64; }
        let color: Color = ColorFromHSV_Divine(hue, sat, val);
        let color2: Color = ColorFromHSV_Divine((hue + 180.0f64) % 360.0f64, sat, val);

        if state {
            ClearBackground(MAROON);
            DrawRectangle(x as i32, y as i32, w as i32, h as i32, color);
            let format: i8* = TextFormat("Congrats! You created your first window! Speed = %.3f.", speed as f64);
            DrawText(format, 200i32, 200i32, 20i32, color2);
        } else {
            ClearBackground(BLACK);
            DrawRectangle(x as i32, y as i32, w as i32, h as i32, { 255u8 - color.r, 255u8 - color.g, 255u8 - color.b } as Color);
            let format: i8* = TextFormat("Congrats! You created your first window! Speed = %.3f.", speed as f64);
            DrawText(format, 200i32, 200i32, 20i32, { 255u8 - color2.r, 255u8 - color2.g, 255u8 - color2.b } as Color);
        }

        DrawFPS(10i32, 10i32);
        EndDrawing();
    }

    CloseWindow();
    ret 0i32;
}