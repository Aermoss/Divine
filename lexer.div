include "divio", "divstr", "divvec", "divmem", "divstd" : *;

enum TokenType {
    THREE_DOT, FLOAT, INTEGER, STRING, CHAR, NAME,
    
    IF, ELSE, FOR, WHILE, FUNC, CLASS, RETURN, BREAK, CONTINUE, CONST, OPERATOR, PUBLIC, PRIVATE,
    INCLUDE, USE, MOD, TYPE, LET, MUT, TRUE, FALSE, NULL, IMPL, NEW, DELETE, EXTERN, ENUM, AS,
    
    PLUS_ASSIGN, MINUS_ASSIGN, MUL_ASSIGN, DIV_ASSIGN, MOD_ASSIGN, LSHIFT_ASSIGN, RSHIFT_ASSIGN, AND_ASSIGN, OR_ASSIGN, XOR_ASSIGN,
    
    QUESTION_MARK, LSHIFT, RSHIFT, AND, BITWISE_AND, OR, BITWISE_OR, BITWISE_XOR, EQUAL, NOT_EQUAL, ARROW, GREATER_EQUAL, LESS_EQUAL, GREATER, LESS, ASSIGN,
    LBRACKET, RBRACKET, LBRACE, RBRACE, LPAREN, RPAREN, PLUS, MINUS, COMMA, MUL, DIV, BACKSLASH, MOD_TOKEN, COLON, SEMICOLON, NOT, BITWISE_NOT, DOT,
    
    EOF, ERROR
};

func TokenTypeToString(t: TokenType) -> String {
    if t == TokenType::THREE_DOT { ret { "THREE_DOT" } as String; }
    else if t == TokenType::FLOAT { ret { "FLOAT" } as String; }
    else if t == TokenType::INTEGER { ret { "INTEGER" } as String; }
    else if t == TokenType::STRING { ret { "STRING" } as String; }
    else if t == TokenType::CHAR { ret { "CHAR" } as String; }
    else if t == TokenType::NAME { ret { "NAME" } as String; }
    else if t == TokenType::IF { ret { "IF" } as String; }
    else if t == TokenType::ELSE { ret { "ELSE" } as String; }
    else if t == TokenType::FOR { ret { "FOR" } as String; }
    else if t == TokenType::WHILE { ret { "WHILE" } as String; }
    else if t == TokenType::FUNC { ret { "FUNC" } as String; }
    else if t == TokenType::CLASS { ret { "CLASS" } as String; }
    else if t == TokenType::RETURN { ret { "RETURN" } as String; }
    else if t == TokenType::BREAK { ret { "BREAK" } as String; }
    else if t == TokenType::CONTINUE { ret { "CONTINUE" } as String; }
    else if t == TokenType::CONST { ret { "CONST" } as String; }
    else if t == TokenType::OPERATOR { ret { "OPERATOR" } as String; }
    else if t == TokenType::PUBLIC { ret { "PUBLIC" } as String; }
    else if t == TokenType::PRIVATE { ret { "PRIVATE" } as String; }
    else if t == TokenType::INCLUDE { ret { "INCLUDE" } as String; }
    else if t == TokenType::USE { ret { "USE" } as String; }
    else if t == TokenType::MOD { ret { "MOD" } as String; }
    else if t == TokenType::TYPE { ret { "TYPE" } as String; }
    else if t == TokenType::LET { ret { "LET" } as String; }
    else if t == TokenType::MUT { ret { "MUT" } as String; }
    else if t == TokenType::TRUE { ret { "TRUE" } as String; }
    else if t == TokenType::FALSE { ret { "FALSE" } as String; }
    else if t == TokenType::NULL { ret { "NULL" } as String; }
    else if t == TokenType::IMPL { ret { "IMPL" } as String; }
    else if t == TokenType::NEW { ret { "NEW" } as String; }
    else if t == TokenType::DELETE { ret { "DELETE" } as String; }
    else if t == TokenType::EXTERN { ret { "EXTERN" } as String; }
    else if t == TokenType::ENUM { ret { "ENUM" } as String; }
    else if t == TokenType::AS { ret { "AS" } as String; }
    else if t == TokenType::PLUS_ASSIGN { ret { "PLUS_ASSIGN" } as String; }
    else if t == TokenType::MINUS_ASSIGN { ret { "MINUS_ASSIGN" } as String; }
    else if t == TokenType::MUL_ASSIGN { ret { "MUL_ASSIGN" } as String; }
    else if t == TokenType::DIV_ASSIGN { ret { "DIV_ASSIGN" } as String; }
    else if t == TokenType::MOD_ASSIGN { ret { "MOD_ASSIGN" } as String; }
    else if t == TokenType::LSHIFT_ASSIGN { ret { "LSHIFT_ASSIGN" } as String; }
    else if t == TokenType::RSHIFT_ASSIGN { ret { "RSHIFT_ASSIGN" } as String; }
    else if t == TokenType::AND_ASSIGN { ret { "AND_ASSIGN" } as String; }
    else if t == TokenType::OR_ASSIGN { ret { "OR_ASSIGN" } as String; }
    else if t == TokenType::XOR_ASSIGN { ret { "XOR_ASSIGN" } as String; }
    else if t == TokenType::QUESTION_MARK { ret { "QUESTION_MARK" } as String; }
    else if t == TokenType::LSHIFT { ret { "LSHIFT" } as String; }
    else if t == TokenType::RSHIFT { ret { "RSHIFT" } as String; }
    else if t == TokenType::AND { ret { "AND" } as String; }
    else if t == TokenType::BITWISE_AND { ret { "BITWISE_AND" } as String; }
    else if t == TokenType::OR { ret { "OR" } as String; }
    else if t == TokenType::BITWISE_OR { ret { "BITWISE_OR" } as String; }
    else if t == TokenType::BITWISE_XOR { ret { "BITWISE_XOR" } as String; }
    else if t == TokenType::EQUAL { ret { "EQUAL" } as String; }
    else if t == TokenType::NOT_EQUAL { ret { "NOT_EQUAL" } as String; }
    else if t == TokenType::ARROW { ret { "ARROW" } as String; }
    else if t == TokenType::GREATER_EQUAL { ret { "GREATER_EQUAL" } as String; }
    else if t == TokenType::LESS_EQUAL { ret { "LESS_EQUAL" } as String; }
    else if t == TokenType::GREATER { ret { "GREATER" } as String; }
    else if t == TokenType::LESS { ret { "LESS" } as String; }
    else if t == TokenType::ASSIGN { ret { "ASSIGN" } as String; }
    else if t == TokenType::LBRACKET { ret { "LBRACKET" } as String; }
    else if t == TokenType::RBRACKET { ret { "RBRACKET" } as String; }
    else if t == TokenType::LBRACE { ret { "LBRACE" } as String; }
    else if t == TokenType::RBRACE { ret { "RBRACE" } as String; }
    else if t == TokenType::LPAREN { ret { "LPAREN" } as String; }
    else if t == TokenType::RPAREN { ret { "RPAREN" } as String; }
    else if t == TokenType::PLUS { ret { "PLUS" } as String; }
    else if t == TokenType::MINUS { ret { "MINUS" } as String; }
    else if t == TokenType::COMMA { ret { "COMMA" } as String; }
    else if t == TokenType::MUL { ret { "MUL" } as String; }
    else if t == TokenType::DIV { ret { "DIV" } as String; }
    else if t == TokenType::BACKSLASH { ret { "BACKSLASH" } as String; }
    else if t == TokenType::MOD_TOKEN { ret { "MOD_TOKEN" } as String; }
    else if t == TokenType::COLON { ret { "COLON" } as String; }
    else if t == TokenType::SEMICOLON { ret { "SEMICOLON" } as String; }
    else if t == TokenType::NOT { ret { "NOT" } as String; }
    else if t == TokenType::BITWISE_NOT { ret { "BITWISE_NOT" } as String; }
    else if t == TokenType::DOT { ret { "DOT" } as String; }
    else if t == TokenType::EOF { ret { "EOF" } as String; }
    else if t == TokenType::ERROR { ret { "ERROR" } as String; }
    else { ret { "UNKNOWN" } as String; }
}

class Token {
    pub _type: TokenType,
    pub value: String,
    pub lineno: i64,
    pub column: i64
};

class Lexer {
    priv text: String,
    priv pos: i64 = 0i64,
    priv lineno: i64 = 1i64,
    priv column: i64 = 1i64,
    priv length: i64 = 0i64
} impl {
    pub IsDigit(this: Lexer&, c: i8) -> bool {
        ret c >= '0' && c <= '9';
    }

    pub IsHexDigit(this: Lexer&, c: i8) -> bool {
        ret (c >= '0' && c <= '9') || (c >= 'a' && c <= 'f') || (c >= 'A' && c <= 'F');
    }

    pub IsBinDigit(this: Lexer&, c: i8) -> bool {
        ret c == '0' || c == '1';
    }

    pub IsOctDigit(this: Lexer&, c: i8) -> bool {
        ret c >= '0' && c <= '7';
    }

    pub IsAlpha(this: Lexer&, c: i8) -> bool {
        ret (c >= 'a' && c <= 'z') || (c >= 'A' && c <= 'Z') || c == '_';
    }

    pub IsAlphaNum(this: Lexer&, c: i8) -> bool {
        ret this.IsAlpha(c) || this.IsDigit(c);
    }

    pub CurrentChar(this: Lexer&) -> i8 {
        if this.pos >= this.length { ret '\0'; }
        ret this.text.Data()[this.pos];
    }

    pub PeekChar(this: Lexer&, offset: i64) -> i8 {
        let peek_pos: i64 = this.pos + offset;
        if peek_pos >= this.length { ret '\0'; }
        ret this.text.Data()[peek_pos];
    }

    pub Advance(mut this: Lexer&) {
        if this.pos < this.length {
            if this.CurrentChar() == '\n' {
                this.lineno += 1i64;
                this.column = 1i64;
            } else {
                this.column += 1i64;
            }

            this.pos += 1i64;
        }
    }

    pub SkipWhitespace(mut this: Lexer&) {
        while this.CurrentChar() == ' ' || this.CurrentChar() == '\t' || this.CurrentChar() == '\r' {
            this.Advance();
        }
    }

    pub SkipComment(mut this: Lexer&) -> bool {
        if this.CurrentChar() == '/' {
            if this.PeekChar(1i64) == '/' {
                while this.CurrentChar() != '\n' && this.CurrentChar() != '\0' {
                    this.Advance();
                } ret true;
            } else if this.PeekChar(1i64) == '*' {
                this.Advance();
                this.Advance();

                while this.CurrentChar() != '\0' {
                    if this.CurrentChar() == '*' && this.PeekChar(1i64) == '/' {
                        this.Advance();
                        this.Advance();
                        break;
                    }

                    this.Advance();
                } ret true;
            }
        }

        ret false;
    }

    pub ReadString(mut this: Lexer&) -> Token {
        let start_pos: i64 = this.pos;
        let start_col: i64 = this.column;
        let quote_char: i8 = this.CurrentChar();
        let mut value: String;
        this.Advance();

        while this.CurrentChar() != quote_char && this.CurrentChar() != '\0' {
            if this.CurrentChar() == '\\' {
                this.Advance();

                if this.CurrentChar() != '\0' {
                    let escaped: i8 = this.CurrentChar();

                    if escaped == 'n' {
                        value += "\n";
                    } else if escaped == 't' {
                        value += "\t";
                    } else if escaped == 'r' {
                        value += "\r";
                    } else if escaped == '\\' {
                        value += "\\";
                    } else if escaped == '\'' {
                        value += "'";
                    } else if escaped == '"' {
                        value += "\"";
                    } else {
                        value += escaped;
                    }

                    this.Advance();
                }
            } else {
                value += this.CurrentChar();
                this.Advance();
            }
        }

        if this.CurrentChar() == quote_char {
            this.Advance();
        }

        if quote_char == '"' {
            ret { TokenType::STRING, value, this.lineno, start_col } as Token;
        } else {
            ret { TokenType::CHAR, value, this.lineno, start_col } as Token;
        }
    }

    pub ReadNumber(mut this: Lexer&) -> Token {
        let mut is_float: bool = false;
        let start_col: i64 = this.column;
        let mut value: String;

        if this.CurrentChar() == '0' {
            value += this.CurrentChar();
            this.Advance();
            
            if this.CurrentChar() == 'x' || this.CurrentChar() == 'X' {
                value += this.CurrentChar();
                this.Advance();

                while this.IsHexDigit(this.CurrentChar()) {
                    value += this.CurrentChar();
                    this.Advance();
                }
            } else if this.CurrentChar() == 'b' || this.CurrentChar() == 'B' {
                value += this.CurrentChar();
                this.Advance();

                while this.IsBinDigit(this.CurrentChar()) {
                    value += this.CurrentChar();
                    this.Advance();
                }
            } else if this.CurrentChar() == 'o' || this.CurrentChar() == 'O' {
                value += this.CurrentChar();
                this.Advance();

                while this.IsOctDigit(this.CurrentChar()) {
                    value += this.CurrentChar();
                    this.Advance();
                }
            } else {
                while this.IsDigit(this.CurrentChar()) {
                    value += this.CurrentChar();
                    this.Advance();
                }
            }
        } else {
            while this.IsDigit(this.CurrentChar()) {
                value += this.CurrentChar();
                this.Advance();
            }
        }

        if this.CurrentChar() == '.' && this.IsDigit(this.PeekChar(1i64)) {
            is_float = true;
            value += this.CurrentChar();
            this.Advance();

            while this.IsDigit(this.CurrentChar()) {
                value += this.CurrentChar();
                this.Advance();
            }
        }

        if this.CurrentChar() == 'e' || this.CurrentChar() == 'E' {
            is_float = true;
            value += this.CurrentChar();
            this.Advance();

            if this.CurrentChar() == '+' || this.CurrentChar() == '-' {
                value += this.CurrentChar();
                this.Advance();
            }

            while this.IsDigit(this.CurrentChar()) {
                value += this.CurrentChar();
                this.Advance();
            }
        }

        if this.CurrentChar() == 'i' || this.CurrentChar() == 'u' {
            value += this.CurrentChar();
            this.Advance();

            while this.IsDigit(this.CurrentChar()) {
                value += this.CurrentChar();
                this.Advance();
            }
        } else if this.CurrentChar() == 'f' {
            is_float = true;
            value += this.CurrentChar();
            this.Advance();

            while this.IsDigit(this.CurrentChar()) {
                value += this.CurrentChar();
                this.Advance();
            }
        }

        if is_float {
            ret { TokenType::FLOAT, value, this.lineno, start_col } as Token;
        } else {
            ret { TokenType::INTEGER, value, this.lineno, start_col } as Token;
        }
    }

    pub ReadIdentifier(mut this: Lexer&) -> Token {
        let mut token_type: TokenType;
        let start_col: i64 = this.column;
        let mut value: String;

        while this.IsAlphaNum(this.CurrentChar()) {
            value += this.CurrentChar();
            this.Advance();
        }

        if value == "if" { token_type = TokenType::IF; }
        else if value == "else" { token_type = TokenType::ELSE; }
        else if value == "for" { token_type = TokenType::FOR; }
        else if value == "while" { token_type = TokenType::WHILE; }
        else if value == "func" { token_type = TokenType::FUNC; }
        else if value == "class" { token_type = TokenType::CLASS; }
        else if value == "ret" { token_type = TokenType::RETURN; }
        else if value == "break" { token_type = TokenType::BREAK; }
        else if value == "continue" { token_type = TokenType::CONTINUE; }
        else if value == "const" { token_type = TokenType::CONST; }
        else if value == "op" { token_type = TokenType::OPERATOR; }
        else if value == "pub" { token_type = TokenType::PUBLIC; }
        else if value == "priv" { token_type = TokenType::PRIVATE; }
        else if value == "include" { token_type = TokenType::INCLUDE; }
        else if value == "use" { token_type = TokenType::USE; }
        else if value == "mod" { token_type = TokenType::MOD; }
        else if value == "type" { token_type = TokenType::TYPE; }
        else if value == "let" { token_type = TokenType::LET; }
        else if value == "mut" { token_type = TokenType::MUT; }
        else if value == "true" { token_type = TokenType::TRUE; }
        else if value == "false" { token_type = TokenType::FALSE; }
        else if value == "null" { token_type = TokenType::NULL; }
        else if value == "impl" { token_type = TokenType::IMPL; }
        else if value == "new" { token_type = TokenType::NEW; }
        else if value == "delete" { token_type = TokenType::DELETE; }
        else if value == "extern" { token_type = TokenType::EXTERN; }
        else if value == "enum" { token_type = TokenType::ENUM; }
        else if value == "as" { token_type = TokenType::AS; }
        else { token_type = TokenType::NAME; }

        ret { token_type, value, this.lineno, start_col } as Token;
    }

    pub ReadOperator(mut this: Lexer&) -> Token {
        let c: i8 = this.CurrentChar();
        let next: i8 = this.PeekChar(1i64);
        let start_col: i64 = this.column;
        let mut token_type: TokenType;
        let mut value: String;

        if c == '.' && next == '.' && this.PeekChar(2i64) == '.' {
            value = "...";
            token_type = TokenType::THREE_DOT;
            this.Advance(); this.Advance(); this.Advance();
        } else if c == '+' {
            if next == '=' {
                value = "+=";
                token_type = TokenType::PLUS_ASSIGN;
                this.Advance(); this.Advance();
            } else {
                value = "+";
                token_type = TokenType::PLUS;
                this.Advance();
            }
        } else if c == '-' {
            if next == '=' {
                value = "-=";
                token_type = TokenType::MINUS_ASSIGN;
                this.Advance(); this.Advance();
            } else if next == '>' {
                value = "->";
                token_type = TokenType::ARROW;
                this.Advance(); this.Advance();
            } else {
                value = "-";
                token_type = TokenType::MINUS;
                this.Advance();
            }
        } else if c == '*' {
            if next == '=' {
                value = "*=";
                token_type = TokenType::MUL_ASSIGN;
                this.Advance(); this.Advance();
            } else {
                value = "*";
                token_type = TokenType::MUL;
                this.Advance();
            }
        } else if c == '/' {
            if next == '=' {
                value = "/=";
                token_type = TokenType::DIV_ASSIGN;
                this.Advance(); this.Advance();
            } else {
                value = "/";
                token_type = TokenType::DIV;
                this.Advance();
            }
        } else if c == '%' {
            if next == '=' {
                value = "%=";
                token_type = TokenType::MOD_ASSIGN;
                this.Advance(); this.Advance();
            } else {
                value = "%";
                token_type = TokenType::MOD_TOKEN;
                this.Advance();
            }
        } else if c == '<' {
            if next == '<' {
                if this.PeekChar(2i64) == '=' {
                    value = "<<=";
                    token_type = TokenType::LSHIFT_ASSIGN;
                    this.Advance(); this.Advance(); this.Advance();
                } else {
                    value = "<<";
                    token_type = TokenType::LSHIFT;
                    this.Advance(); this.Advance();
                }
            } else if next == '=' {
                value = "<=";
                token_type = TokenType::LESS_EQUAL;
                this.Advance(); this.Advance();
            } else {
                value = "<";
                token_type = TokenType::LESS;
                this.Advance();
            }
        } else if c == '>' {
            if next == '>' {
                if this.PeekChar(2i64) == '=' {
                    value = ">>=";
                    token_type = TokenType::RSHIFT_ASSIGN;
                    this.Advance(); this.Advance(); this.Advance();
                } else {
                    value = ">>";
                    token_type = TokenType::RSHIFT;
                    this.Advance(); this.Advance();
                }
            } else if next == '=' {
                value = ">=";
                token_type = TokenType::GREATER_EQUAL;
                this.Advance(); this.Advance();
            } else {
                value = ">";
                token_type = TokenType::GREATER;
                this.Advance();
            }
        } else if c == '&' {
            if next == '&' {
                value = "&&";
                token_type = TokenType::AND;
                this.Advance(); this.Advance();
            } else if next == '=' {
                value = "&=";
                token_type = TokenType::AND_ASSIGN;
                this.Advance(); this.Advance();
            } else {
                value = "&";
                token_type = TokenType::BITWISE_AND;
                this.Advance();
            }
        } else if c == '|' {
            if next == '|' {
                value = "||";
                token_type = TokenType::OR;
                this.Advance(); this.Advance();
            } else if next == '=' {
                value = "|=";
                token_type = TokenType::OR_ASSIGN;
                this.Advance(); this.Advance();
            } else {
                value = "|";
                token_type = TokenType::BITWISE_OR;
                this.Advance();
            }
        } else if c == '^' {
            if next == '=' {
                value = "^=";
                token_type = TokenType::XOR_ASSIGN;
                this.Advance(); this.Advance();
            } else {
                value = "^";
                token_type = TokenType::BITWISE_XOR;
                this.Advance();
            }
        } else if c == '=' {
            if next == '=' {
                value = "==";
                token_type = TokenType::EQUAL;
                this.Advance(); this.Advance();
            } else {
                value = "=";
                token_type = TokenType::ASSIGN;
                this.Advance();
            }
        } else if c == '!' {
            if next == '=' {
                value = "!=";
                token_type = TokenType::NOT_EQUAL;
                this.Advance(); this.Advance();
            } else {
                value = "!";
                token_type = TokenType::NOT;
                this.Advance();
            }
        } else if c == '?' {
            value = "?";
            token_type = TokenType::QUESTION_MARK;
            this.Advance();
        } else if c == '~' {
            value = "~";
            token_type = TokenType::BITWISE_NOT;
            this.Advance();
        } else if c == '.' {
            value = ".";
            token_type = TokenType::DOT;
            this.Advance();
        } else if c == ',' {
            value = ",";
            token_type = TokenType::COMMA;
            this.Advance();
        } else if c == ':' {
            value = ":";
            token_type = TokenType::COLON;
            this.Advance();
        } else if c == ';' {
            value = ";";
            token_type = TokenType::SEMICOLON;
            this.Advance();
        } else if c == '(' {
            value = "(";
            token_type = TokenType::LPAREN;
            this.Advance();
        } else if c == ')' {
            value = ")";
            token_type = TokenType::RPAREN;
            this.Advance();
        } else if c == '[' {
            value = "[";
            token_type = TokenType::LBRACKET;
            this.Advance();
        } else if c == ']' {
            value = "]";
            token_type = TokenType::RBRACKET;
            this.Advance();
        } else if c == '{' {
            value = "{";
            token_type = TokenType::LBRACE;
            this.Advance();
        } else if c == '}' {
            value = "}";
            token_type = TokenType::RBRACE;
            this.Advance();
        } else if c == '\\' {
            value = "\\";
            token_type = TokenType::BACKSLASH;
            this.Advance();
        } else {
            value = c;
            token_type = TokenType::ERROR;
            this.Advance();
        }

        ret { token_type, value, this.lineno, start_col } as Token;
    }

    pub NextToken(mut this: Lexer&) -> Token {
        while this.CurrentChar() != '\0' {
            this.SkipWhitespace();

            if this.CurrentChar() == '\0' {
                break;
            }

            if this.CurrentChar() == '\n' {
                this.Advance();
                continue;
            }

            if this.SkipComment() {
                continue;
            }

            if this.CurrentChar() == '"' || this.CurrentChar() == '\'' {
                ret this.ReadString();
            }

            if this.IsDigit(this.CurrentChar()) || (this.CurrentChar() == '.' && this.IsDigit(this.PeekChar(1i64))) {
                ret this.ReadNumber();
            }

            if this.IsAlpha(this.CurrentChar()) {
                ret this.ReadIdentifier();
            }

            ret this.ReadOperator();
        }

        ret { TokenType::EOF, "", this.lineno, this.column } as Token;
    }

    pub Lex(mut this: Lexer&, text: String) -> Vector<Token> {
        (this.text, this.length) = (text, text.Size());
        (this.pos, this.lineno, this.column) = (0i64, 1i64, 1i64);

        let mut tokens: Vector<Token>;
        let mut token: Token = this.NextToken();

        while token._type != TokenType::EOF && token._type != TokenType::ERROR {
            tokens.PushBack(token);
            token = this.NextToken();
        }

        ret tokens;
    }
};

func ReadFile(path: i8*) -> String {
    let file: FILE* = fopen(path, "r");

    if file == null {
        printf("Failed to open file: '%s'.\n", path);
        exit(1i32);
    }

    let buffer[256i64]: i8;
    let mut content: String;

    while fgets(buffer, sizeof(buffer) as i32, file) != null {
        content += buffer;
    }

    fclose(file);
    ret content;
}

func main(argc: i32, argv[]: i8*) -> i32 {
    if argc < 2i32 {
        printf("Usage: .\\lexer.exe <source_file>\n");
        ret 1i32;
    }

    let mut lexer: Lexer;
    let source: String = ReadFile(argv[1i64]);
    let tokens: Vector<Token> = lexer.Lex(source);
    let mut index: i64 = 0i64;

    while tokens.Size() > index {
        let token: Token = tokens.Data()[index];
        printf("Token: Type=%s, Value='%s', Line=%d, Column=%d\n", TokenTypeToString(token._type).Data(), token.value.Data(), token.lineno, token.column);
        index += 1i64;
    }

    ret 0i32;
}