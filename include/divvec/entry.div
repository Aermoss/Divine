include "divmem" : *;

class Vector<T> {
    priv data: T* = null,
    priv size: i64 = 0i64
} impl {
    Vector() {}

    Vector(other: Vector<T>*) {
        Allocate(other->Size());
        if other->Data() == null { ret; }
        memcpy(data, other->Data(), sizeof(T) * size);
    }

    ~Vector() {
        Release();
    }

    pub Data() -> T* { ret data; }
    pub Size() -> i64 { ret size; }

    pub Allocate(newSize: i64) {
        (data, size) = (malloc(sizeof(T) * newSize), newSize);
    }

    pub Release() {
        if data != null { free(data); }
        (data, size) = (null, 0i64);
    }

    pub PushBack(value: T) {
        let temp: T* = data;
        this->Allocate(size + 1i64);

        if temp != null {
            memcpy(data, temp, sizeof(T) * (size - 1i64));
        }

        free(temp);
        data[size - 1i64] = value;
    }

    pub PopBack() -> T {
        let temp: T* = data;
        this->Allocate(size - 1i64);
        let value: T = temp[size];

        if temp != null {
            memcpy(data, temp, sizeof(T) * size);
        }

        free(temp);
        ret value;
    }
};