include "divmem" : *;

func max(a: i64, b: i64) -> i64 {
    if a > b { ret a; }
    else { ret b; }
}

class String {
    priv data: i8* = null,
    priv size: i64 = 0i64
} impl {
    String(string: i8*) {
        if string == null { ret; }
        if data != null { free(data); }
        Allocate(strlen(string) + 1i64);
        memcpy(data, string, size);
    }

    ~String() {
        Release();
    }

    pub Data() -> i8* { ret data; }
    pub Size() -> i64 { ret size; }

    priv Allocate(newSize: i64) {
        (data, size) = (malloc(newSize), newSize);
    }

    priv Release() {
        if data != null { free(data); }
        (data, size) = (null, 0i64);
    }

    pub op=(string: i8*) {
        if string == null { ret; }
        if data != null { free(data); }
        Allocate(strlen(string) + 1i64);
        memcpy(data, string, size);
    }

    pub op+=(string: i8*) {
        if string == null { ret; }
        let (oldData: i8*, oldSize: i64) = (data, max(size - 1i64, 0i64));
        Allocate(oldSize + strlen(string) + 1i64);

        if oldData != null {
            memcpy(data, oldData, oldSize);
            free(oldData);
        }

        memcpy(data + oldSize, string, strlen(string) + 1i64);
    }
};