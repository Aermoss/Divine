class SharedPtr<T> {
    priv ptr: T* = null,
    priv count: u64* = null
} impl {
    SharedPtr(mut this: SharedPtr<T>&) {
        this.SharedPtr(new T);
    }

    SharedPtr(mut this: SharedPtr<T>&, ptr: T*) {
        this.ptr = ptr;

        if ptr != null {
            this.count = new u64;
            *this.count = 1u64;
        } else {
            this.count = null;
        }
    }

    SharedPtr(mut this: SharedPtr<T>&, other: SharedPtr<T>&) {
        (this.ptr, this.count) = (other.ptr, other.count);

        if this.count != null {
            *this.count += 1u64;
        }
    }

    ~SharedPtr(mut this: SharedPtr<T>&) {
        this.Release();
    }

    pub Ptr(this: SharedPtr<T>&) -> T* { ret this.ptr; }
    pub Count(this: SharedPtr<T>&) -> u64 { ret (this.count != null) ? (*this.count) : 0u64; }
     /* ^^^^^ crashes because tries to dereference null pointer before checking for null pointer */

    priv Release(mut this: SharedPtr<T>&) {
        if (this.count != null) {
            *this.count -= 1u64;

            if (*this.count == 0u64) {
                del this.ptr;
                del this.count;
            }

            (this.ptr, this.count) = (null, null);
        }
    }

    pub op=(mut this: SharedPtr<T>&, other: SharedPtr<T>&) -> SharedPtr<T>&{
        if this != other {
            this.Release();
            (this.ptr, this.count) = (other.ptr, other.count);
            if this.count != null { *this.count += 1u64; }
        } ret this;
    }
};